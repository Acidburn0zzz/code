#
# $Id$
#
# Additional procedures needed by NXtranslate GENIE
#
# Freddie Akeroyd <F.A.Akeroyd@rl.ac.uk>
#
PRINTIN "Loading additional OpenGENIE  procedures"

PROCEDURE GETDAT
QUALIFIERS /GANG # want values by spectrum
PARAMETERS name=String
RESULT RES
GLOBAL CNT1 SPEC UDET NSP NDET MDET NTC sorted_spec LEN2
LOCAL TMP dstidx i j
IF NOT DEFINED(CNT1)
    SPEC <~ GET("SPEC")
    sorted_spec = spec
    sort rank(spec) sorted_spec
    UDET <~ GET("UDET")
    nsp <~ GET("NSP1")
    ntc <~ GET("NTC1")
    tmp <~ GET("CNT1")
    cnt1 <~ tmp[2:(nsp+1),2:(ntc+1)] # remove spectrum 0 and time channel 0
    mdet <~ GET("MDET")
    ndet <~ GET("NDET")
    len2 <~ GET("LEN2")
ENDIF
IF name = "CNT1"
    res <~ cnt1
    RETURN
ENDIF
dstidx <~ dimensions(nsp)
fill dstidx 1 1
IF name = "MON1_DET"
    i <~ MDET[1]
    IF GANG
        res <~ SPEC[i]
    ELSE
        res <~ UDET[i]
    ENDIF
    RETURN
ENDIF
IF name = "MON1_DIST"
    i <~ MDET[1]
    res <~ len2[i]
    RETURN
ENDIF
IF name = "MON1"
    i <~ MDET[1]
    j <~ SPEC[i]
    RES <~ redim(CNT1[j,1:(ntc)],ntc)
    RETURN
ENDIF
IF GANG AND (name = "UDET")
    RES <~ DSTIDX
    RETURN
ENDIF
IF (name = "GROUP_INDEX")
    dstidx <~ dimensions(ndet)
    fill dstidx 1
    RES <~ DSTIDX
    RETURN
ENDIF
IF (name = "GANG_COUNT") OR (name = "GANG_INDEX")
    TMP <~ UNIQUE_ELEMENTS:IGNMISS(sorted_spec, dstidx)
    IF name = "GANG_COUNT"
        res <~ tmp.counts
    ENDIF
    IF name = "GANG_INDEX"
        res <~ tmp.offsets
    ENDIF
    RETURN
ENDIF
TMP <~ GET(NAME)
IF GANG
    RES <~ INDEXEDCOPY:ZEROMISSING:AVERAGE:IGNSRCDUP(dstidx,spec,tmp)
ELSE
    SORT RANK(SPEC) TMP 
    RES <~ TMP
ENDIF
ENDPROCEDURE

